  module typeCrysPlasMatPt
    use utils, only : RKIND, IKIND, LKIND
    use typeCrysPlasMat, only : CrysPlasMat

    implicit none

    integer(kind = RKIND), parameter :: kConvgIter   = 1
    integer(kind = RKIND), parameter :: kPertubation = 2
    integer(kind = RKIND), parameter :: kNumGrains   = 1

    type, public :: CrysPlasMatPt
      ! Grain Averaged variables at the current material point(integeration point)
      real(kind = RKIND)    :: fAvgSig(6) 
      real(kind = RKIND)    :: fAvgCijkl(6, 6) 
      real(kind = RKIND)    :: fAvgDSigDTemp(6) 
      real(kind = RKIND)    :: fAvgRpl 
      real(kind = RKIND)    :: fAvgDRplDEps(6) 
      real(kind = RKIND)    :: fAvgDRplDTemp 

      real(kind = RKIND)    :: fNewDt
      real(kind = RKIND)    :: fDt
      logical(kind = LKIND) :: fIsTempDep

      class(CrysPlasMat),  pointer :: fCrysPlasMat
      integer(kind = IKIND) :: fNumSlipSys
      integer(kind = RKIND) :: fNumGrains
      integer(kind = RKIND) :: fNumVarsPerGrn

      !* Variables for Current Grain
      real(kind = RKIND)    :: fCrysSig(6)
      real(kind = RKIND)    :: fCrysCijkl(6, 6)
      real(kind = RKIND)    :: fCrysDSigDTemp(6)
      real(kind = RKIND)    :: fCrysRpl
      real(kind = RKIND)    :: fCrysDRplDEps(6) 
      real(kind = RKIND)    :: fCrysDRplDTemp 

      real(kind = RKIND)    :: fTempOld  
      real(kind = RKIND)    :: fFtotOld(3, 3)    

      real(kind = RKIND)    :: fTempNew  
      real(kind = RKIND)    :: fFtotNew(3, 3)  

      real(kind = RKIND)    :: fFElsOld(3, 3)  
      real(kind = RKIND)    :: fLPlsOld(3, 3)  
      real(kind = RKIND), allocatable :: fTauCritOld(:)  
      real(kind = RKIND), allocatable :: fGammaOld(:)  

      real(kind = RKIND)    :: fFElsNew(3, 3)  
      real(kind = RKIND)    :: fLPlsNew(3, 3)  
      real(kind = RKIND), allocatable :: fTauCritNew(:)  
      real(kind = RKIND), allocatable :: fGammaNew(:)  
  
      real(kind = RKIND)    :: fRotMatx(3, 3)
      real(kind = RKIND)    :: fEulerAng(3)

      real(kind = RKIND), allocatable :: fTauResl(:)
      real(kind = RKIND), allocatable :: fGammaDot(:)
      real(kind = RKIND), allocatable :: fDGamma(:)  

      real(kind = RKIND)    :: fCijkl(3, 3, 3, 3)
      real(kind = RKIND), allocatable :: fSchmidt(:, :, :)


    contains
      procedure, public  :: InitMatPt
      procedure, public  :: AdvanceStep
      procedure, public  :: InitStateVar
      procedure, public  :: SetCrysOldState
      procedure, public  :: SetCrysNewState
      procedure, public  :: GetCrysCurState
      procedure, public  :: LoadCrysState
      procedure, public  :: SaveCrysState
      procedure, public  :: GetCauchyStress
      procedure, public  :: GetMatJacob
      procedure, public  :: GetDSigDTemp
      procedure, public  :: GetHeatGenRate
      procedure, public  :: GetDRplDEps
      procedure, public  :: GetDRplDTemp
      procedure, public  :: GetNewDtScaleFactor

      procedure, private :: UpdateStress
      procedure, private :: Pertubation

      procedure, private :: GreenStrain
      procedure, private :: ElasticStress
      procedure, private :: ResolvedSlipStress
      procedure, private :: VelocityGradientPlastic

      procedure, private :: InitXX
      procedure, private :: FormLHS
      procedure, private :: FormRHS
      procedure, private :: UpdateXX
      procedure, private :: Correction

      procedure, private :: PreStep
      procedure, private :: SolveStep
      procedure, private :: PostStep

      !procedure, private :: FormRotMatx
      final              :: Destructor

    end type CrysPlasMatPt


    public
    interface CrysPlasMatPt
      module procedure Constructor
    end interface

  contains
  
    function Constructor() result(this)
      use  algebra, only : UNITMAT
      type(CrysPlasMat),   pointer :: pCrysPlasMat
      type(CrysPlasMatPt), pointer :: this
  

      allocate(this)

      this%fCrysPlasMat => NULL()
      
    end function Constructor
          


    subroutine InitMatPt(this, pCrysPlasMat, ansyType)
      use  algebra, only : UNITMAT

      class(CrysPlasMatPt) :: this
      class(CrysPlasMat), pointer :: pCrysPlasMat
      integer(kind = IKIND), intent(in) :: ansyType
  

      this%fCrysPlasMat => pCrysPlasMat

      if (ansyType == 0) then
        this%fIsTempDep  = .false.
      else
        this%fIsTempDep  = .true.
      end if

      this%fNumSlipSys  = this%fCrysPlasMat%GetNumSlipSys()
      this%fNumGrains   = kNumGrains
      this%fNumVarsPerGrn = 30 + 2*this%fNumSlipSys

      this%fTempOld    = 300
      this%fTempNew    = this%fTempOld
      this%fFtotOld    = UNITMAT
      this%fFtotNew    = this%fFtotOld

      this%fDt         = 0.0d0
      this%fNewDt      = 1.0d0

      this%fAvgSig        = 0.0d0
      this%fAvgCijkl      = 0.0d0
      this%fAvgRpl        = 0.0d0
      this%fAvgDSigDTemp  = 0.0d0
      this%fAvgDRplDEps   = 0.0d0
      this%fAvgDRplDTemp  = 0.0d0

      !* Variables for Current Grain
      this%fCrysSig       = 0.0d0
      this%fCrysCijkl     = 0.0d0
      this%fCrysDSigDTemp = 0.0d0
      this%fCrysRpl       = 0.0d0
      this%fCrysDRplDEps  = 0.0d0
      this%fCrysDRplDTemp = 0.0d0 

      allocate(this%fTauCritOld(this%fNumSlipSys))  
      allocate(this%fTauCritNew(this%fNumSlipSys))  
      allocate(this%fGammaOld(this%fNumSlipSys))  
      allocate(this%fGammaNew(this%fNumSlipSys))  
  
      this%fFElsOld    = UNITMAT
      this%fLPlsOld    = 0.0d0
      this%fTauCritOld = this%fCrysPlasMat%GetInitialCRSS()
      this%fGammaOld   = 0.0d0
  
      this%fFElsNew    = this%fFElsOld
      this%fLPlsNew    = this%fLPlsOld
      this%fTauCritNew = this%fTauCritOld
      this%fGammaNew   = this%fGammaOld

      this%fRotMatx    = UNITMAT
      this%fEulerAng   = (/0.0d0, 0.0d0, 0.0d0/)

      allocate(this%fTauResl(this%fNumSlipSys))
      this%fTauResl = 0.0d0

      allocate(this%fGammaDot(this%fNumSlipSys))
      this%fGammaDot = 0.0d0

      allocate(this%fDGamma(this%fNumSlipSys))  
      this%fDGamma  = 0.0d0

      allocate(this%fSchmidt(3, 3, this%fNumSlipSys))
      this%fSchmidt = 0.0d0

    end subroutine InitMatPt




    subroutine Destructor(this)
      type(CrysPlasMatPt) :: this

      if (allocated(this%fTauCritOld))  deallocate(this%fTauCritOld)
      if (allocated(this%fTauCritNew))  deallocate(this%fTauCritNew)
      if (allocated(this%fGammaOld))    deallocate(this%fGammaOld)
      if (allocated(this%fGammaNew))    deallocate(this%fGammaNew)
      if (allocated(this%fTauResl))     deallocate(this%fTauResl)
      if (allocated(this%fGammaDot))    deallocate(this%fGammaDot)
      if (allocated(this%fDGamma))      deallocate(this%fDGamma)
      if (allocated(this%fSchmidt))     deallocate(this%fSchmidt)

    end subroutine Destructor


   

 !   subroutine FormRotMatx(this, oriArray)
 !     use algebra, only : normalize, cross_product
 ! 
 !     class(CrysPlasMatPt)           :: this
 !     real(kind = RKIND), intent(in) :: oriArray(6)
 ! 
 !     real(kind = RKIND) :: auxVec1(3)
 !     real(kind = RKIND) :: auxVec2(3)
 ! 
 !     auxVec1 = oriArray(1:3)
 !     auxVec2 = oriArray(4:6)
 ! 
 !     call normalize(auxVec1)
 !     call normalize(auxVec2)
 ! 
 !     this%fRotMatx(:, 1) = auxVec1
 !     this%fRotMatx(:, 2) = auxVec2
 !     this%fRotMatx(:, 3) = cross_product(this%fRotMatx(:, 1), this%fRotMatx(:, 2))
 ! 
 !   end subroutine FormRotMatx
  


    subroutine InitStateVar(this, oriArray, statev, nstatv)
      use algebra,  only : normalize, cross_product, AnglesToRotMatrix, PI

      class(CrysPlasMatPt)               :: this
      real(kind = RKIND),    intent(in)  :: oriArray(6)   
      integer(kind = IKIND), intent(in)  :: nstatv
      real(kind = RKIND)                 :: statev(nstatv)

      real(kind = RKIND)    :: angle(3)
      real(kind = RKIND)    :: rotMatx(3, 3)
      real(kind = RKIND)    :: crysStateVars(this%fNumVarsPerGrn)
      integer(kind = IKIND) :: iGrn
  
  

      if (this%fNumGrains == 1) then
        rotMatx(:, 1) = oriArray(1:3)
        rotMatx(:, 2) = oriArray(4:6)
  
        call normalize(rotMatx(1, :))
        call normalize(rotMatx(2, :))
        rotMatx(:, 3) = cross_product(rotMatx(:, 1), rotMatx(:, 2))
  
        this%fRotMatx = rotMatx

        iGrn = 1
        call this%GetCrysCurState(crysStateVars)
        call this%SaveCrysState(iGrn, crysStateVars, statev, nstatv)
      else
        do iGrn = 1, this%fNumGrains
          call random_seed()
          call random_number(angle)
          angle(1) = angle(1)*2.0d0*PI
          angle(2) = angle(2)*PI
          angle(3) = angle(3)*2.0d0*PI

          rotMatx  = AnglesToRotMatrix(angle)
          this%fRotMatx = rotMatx

          call this%GetCrysCurState(crysStateVars)
          call this%SaveCrysState(iGrn, crysStateVars, statev, nstatv)
         end do

       end if

    end subroutine InitStateVar


    subroutine LoadCrysState(this, statev, nstatv, iGrain, crysStateVars)  
      class(CrysPlasMatPt)               :: this
      integer(kind = IKIND), intent(in)  :: nstatv
      real(kind = RKIND),    intent(in)  :: statev(nstatv)
      integer(kind = IkIND), intent(in)  :: iGrain
      real(kind = RKIND),    intent(out) :: crysStateVars(this%fNumVarsPerGrn)

      integer(kind = IKIND) :: bgnLoc, endLoc

      ! Begin location for the current grain
      bgnLoc = (iGrain - 1) * this%fNumVarsPerGrn + 1
      endLoc = bgnLoc + this%fNumVarsPerGrn - 1

      crysStateVars = statev(bgnLoc : endLoc)

    end subroutine LoadCrysState



    subroutine SetCrysOldState(this, crysStateVars)
      class(CrysPlasMatPt)            :: this
      real(kind = RKIND),  intent(in) :: crysStateVars(this%fNumVarsPerGrn)

      integer(kind = IKIND) :: arrLen, idxLo, idxHi

      arrLen = 9
      idxLo = 1
      idxHi = idxLo + arrLen - 1
      this%fFElsOld = reshape(crysStateVars(idxLo : idxHi), (/3, 3/))  
  
    
      arrLen = this%fNumSlipSys
      idxLo  = idxHi + 1
      idxHi  = idxLo + arrLen - 1
      this%fGammaOld = crysStateVars(idxLo : idxHi)
  
      arrLen = this%fNumSlipSys
      idxLo  = idxHi + 1
      idxHi  = idxLo + arrLen - 1
      this%fTauCritOld = crysStateVars(idxLo : idxHi)
  
      arrLen = 9
      idxLo  = idxHi + 1
      idxHi  = idxLo + arrLen - 1
      this%fLPlsOld = reshape(crysStateVars(idxLo : idxHi), (/3, 3/))

      arrLen = 9
      idxLo  = idxHi + 1
      idxHi  = idxLo + arrLen - 1
      this%fRotMatx = reshape(crysStateVars(idxLo: idxHi), (/3, 3/))

      arrLen = 3
      idxLo  = idxHi + 1
      idxHi  = idxLo + arrLen - 1
      this%fEulerAng = crysStateVars(idxLo: idxHi)

    end subroutine SetCrysOldState



    subroutine SetCrysNewState(this, crysStateVars)
      class(CrysPlasMatPt)              :: this
      real(kind = RKIND),    intent(in) :: crysStateVars(this%fNumVarsPerGrn)

      integer(kind = IKIND) :: arrLen, idxLo, idxHi

      arrLen = 9
      idxLo = 1
      idxHi = idxLo + arrLen - 1
      this%fFElsNew = reshape(crysStateVars(idxLo : idxHi), (/3, 3/))  
  
    
      arrLen = this%fNumSlipSys
      idxLo  = idxHi + 1
      idxHi  = idxLo + arrLen - 1
      this%fGammaNew = crysStateVars(idxLo : idxHi)
  
      arrLen = this%fNumSlipSys
      idxLo  = idxHi + 1
      idxHi  = idxLo + arrLen - 1
      this%fTauCritNew = crysStateVars(idxLo : idxHi)
  
      arrLen = 9
      idxLo  = idxHi + 1
      idxHi  = idxLo + arrLen - 1
      this%fLPlsNew = reshape(crysStateVars(idxLo : idxHi), (/3, 3/))

      arrLen = 9
      idxLo  = idxHi + 1
      idxHi  = idxLo + arrLen - 1
      this%fRotMatx = reshape(crysStateVars(idxLo: idxHi), (/3, 3/))
  

      arrLen = 3
      idxLo  = idxHi + 1
      idxHi  = idxLo + arrLen - 1
      this%fEulerAng = crysStateVars(idxLo: idxHi)

    end subroutine SetCrysNewState


    subroutine GetCrysCurState(this, crysStateVars)
      class(crysPlasMatPt),  intent(in)  :: this
      real(kind = RKIND),    intent(out) :: crysStateVars(this%fNumVarsPerGrn)

      integer(kind = IKIND) :: arrLen, idxLo, idxHi

      arrLen = 9
      idxLo = 1
      idxHi = idxLo + arrLen - 1
      crysStateVars(idxLo : idxHi)   = reshape(this%fFElsNew, (/9/))
  
        
      arrLen = this%fNumSlipSys
      idxLo  = idxHi + 1
      idxHi  = idxLo + arrLen - 1
      crysStateVars(idxLo : idxHi) = this%fGammaNew
  
      arrLen = this%fNumSlipSys
      idxLo  = idxHi + 1
      idxHi  = idxLo + arrLen - 1
      crysStateVars(idxLo  :  idxHi)  = this%fTauCritNew
  
      arrLen = 9
      idxLo  = idxHi + 1
      idxHi  = idxLo + arrLen - 1
      crysStateVars(idxLo : idxHi) = reshape(this%fLPlsNew, (/9/)) 

      arrLen = 9
      idxLo  = idxHi + 1
      idxHi  = idxLo + arrLen - 1
      crysStateVars(idxLo : idxHi) = reshape(this%fRotMatx, (/9/)) 
  
      arrLen = 3
      idxLo  = idxHi + 1
      idxHi  = idxLo + arrLen - 1
      crysStateVars(idxLo: idxHi) = this%fEulerAng
  
    end subroutine GetCrysCurState
  

  
    subroutine SaveCrysState(this, iGrain, crysStateVars, statev, nstatv)
      class(crysPlasMatPt),  intent(in)  :: this
      integer(kind = IKIND), intent(in)  :: iGrain
      real(kind = RKIND),    intent(in)  :: crysStateVars(this%fNumVarsPerGrn)
      integer(kind = IKIND), intent(in)  :: nstatv
      real(kind = RKIND),    intent(out) :: statev(nstatv)
  
      integer(kind = IKIND) :: bgnLoc, endLoc
  
      ! Begin location for the current grain
      bgnLoc = (iGrain - 1) * this%fNumVarsPerGrn + 1
      endLoc = bgnLoc + this%fNumVarsPerGrn - 1 

      statev(bgnLoc : endLoc) = crysStateVars

    end subroutine SaveCrysState


      
            
    subroutine AdvanceStep(this, FTotOld, FTotNew, TempOld, TempNew, statev, nstatv, dtime)
      use algebra, only : Ten3333ToA66

      class(CrysPlasMatPt) :: this
      real(kind = RKIND),    intent(in) :: FTotOld(3, 3)
      real(kind = RKIND),    intent(in) :: FTotNew(3, 3)
      real(kind = RKIND),    intent(in) :: TempOld
      real(kind = RKIND),    intent(in) :: TempNew
      integer(kind = IKIND), intent(in) :: nstatv
      real(kind = RKIND),    intent(in) :: dtime
      real(kind = RKIND),    intent(inout) :: statev(nstatv)

      real(kind = RKIND) :: crysOldStateVars(this%fNumVarsPerGrn)
      real(kind = RKIND) :: crysNewStateVars(this%fNumVarsPerGrn)
      real(kind = RKIND) :: crysCvgStateVars(this%fNumVarsPerGrn)

      integer(kind = IKIND) :: stage
      integer(kind = IKIND) :: iGrn


      this%fDt      = dtime
      this%fFTotOld = FTotOld
      this%fFTotNew = FTotNew
      this%fTempOld = TempOld
      this%fTempNew = TempNew

      do iGrn = 1, this%fNumGrains
        call this%LoadCrysState(statev, nstatv, iGrn, crysOldStateVars)
        crysNewStateVars = crysOldStateVars

        stage = kConvgIter
        call this%UpdateStress(crysOldStateVars, crysNewStateVars, stage)

        this%fAvgSig = this%fAvgSig + this%fCrysSig
        this%fAvgRpl = this%fAvgRpl + this%fCrysRpl

        call this%GetCrysCurState(crysCvgStateVars)

        ! pertubation method to calculate ddsdde and drplde
        stage = kPertubation
        call this%Pertubation(crysOldStateVars, crysCvgStateVars, stage)
        this%fAvgCijkl = this%fAvgCijkl + this%fCrysCijkl
      
        call this%SaveCrysState(iGrn, crysCvgStateVars, statev, nstatv)
      end do

    end subroutine AdvanceStep



    function GetCauchyStress(this) result(sigma)
      class(CrysPlasMatPt) :: this
      real(kind = RKIND)   :: sigma(6)

      sigma = this%fAvgSig / this%fNumGrains


    end function GetCauchyStress



    function GetMatJacob(this) result(jacob)
      class(CrysPlasMatPt) :: this
      real(kind = RKIND)   :: jacob(6, 6)

      jacob = this%fAvgCijkl / this%fNumGrains

    end function GetMatJacob


    function GetDSigDTemp(this) result(ddsddt)
      class(CrysPlasMatPt) :: this
      real(kind = RKIND)   :: ddsddt(6)

      ddsddt = this%fAvgDSigDTemp / this%fNumGrains

    end function GetDSigDTemp

    

    function GetHeatGenRate(this) result(rpl)
      class(CrysPlasMatPt) :: this
      real(kind = RKIND)   :: rpl

      rpl = this%fAvgRpl / this%fNumGrains

    end function


    function GetDRplDTemp(this) result(dRplDT)
      class(CrysPlasMatPt) :: this
      real(kind = RKIND)   :: dRplDT

      dRplDT = this%fAvgDRplDTemp / this%fNumGrains

    end function GetDRplDTemp



    function GetDRplDEps(this) result(dRplDE)
      class(CrysPlasMatPt) :: this
      real(kind = RKIND)   :: dRplDE(6)

      dRplDE = this%fAvgDRplDEps / this%fNumGrains

    end function GetDRplDEps



    function GetNewDtScaleFactor(this) result(pNewDt)
      class(CrysPlasMatPt) :: this
      real(kind = RKIND)   :: pNewDt

      pNewDt = this%fNewDt

    end function GetNewDtScaleFactor



    subroutine UpdateStress(this, crysOldStateVars, crysNewStateVars, stage)
      class(CrysPlasMatPt) :: this
      real(kind = RKIND), intent(in)    :: crysOldStateVars(this%fNumVarsPerGrn) 
      real(kind = RKIND), intent(in)    :: crysNewStateVars(this%fNumVarsPerGrn) 
      integer(kind = IKIND), intent(in) :: stage

      call this%SetCrysOldState(crysOldStateVars)
      call this%SetCrysNewState(crysNewStateVars)

      call this%PreStep(stage)
      call this%SolveStep(stage)
      call this%PostStep(stage)

    end subroutine UpdateStress




    subroutine Pertubation(this, crysOldStateVars, crysNewStateVars, stage)
      use algebra, only : UNITMAT, Ten3333ToA66

      class(CrysPlasMatPt) :: this
      real(kind = RKIND), intent(in)    :: crysOldStateVars(this%fNumVarsPerGrn) 
      real(kind = RKIND), intent(in)    :: crysNewStateVars(this%fNumVarsPerGrn) 
      integer(kind = IKIND), intent(in) :: stage

      real(kind = RKIND), parameter :: kEpsIncPer  = 5.0d-5
      real(kind = RKIND), parameter :: kTempIncPer = 1.0d-3

      real(kind = RKIND) :: halfEpsInc
      real(kind = RKIND) :: FTotInc(3, 3)
      real(kind = RKIND) :: FTotPer(3, 3)
      real(kind = RKIND) :: dltEpsPer(3, 3)
      real(kind = RKIND) :: tempPer

      real(kind = RKIND) :: sigCvg(6)
      real(kind = RKIND) :: rplCvg
      real(kind = RKIND) :: sigEpsPer(6)
      real(kind = RKIND) :: rplEpsPer
      real(kind = RKIND) :: sigTempPerPlus(6)
      real(kind = RKIND) :: sigTempPerMinus(6)
      real(kind = RKIND) :: rplTempPerPlus
      real(kind = RKIND) :: rplTempPerMinus

      real(kind = RKIND)    :: dGammaCvg(this%fNumSlipSys)
      real(kind = RKIND)    :: dEpsDT(6)
      integer(kind = IKIND) :: idx1, idx2, idxi(6), idxj(6), iEpsPer



      if (this%fNewDt < 1.0d0) return

      sigCvg    = this%fCrysSig
      rplCvg    = this%fCrysRpl
      dGammaCvg = this%fDGamma

      ! General loop on the 6 perturbations to obtain Jacobian
      halfEpsInc = 0.5d0*kEpsIncPer
      dltEpsPer  = reshape((/kEpsIncPer, halfEpsInc, halfEpsInc,             &
                             halfEpsInc, kEpsIncPer, halfEpsInc,             &
                             halfEpsInc, halfEpsInc, kEpsIncPer/), (/3, 3/))

      idxi = (/1, 2, 3, 1, 1, 2/)
      idxj = (/1, 2, 3, 2, 3, 3/)
      do iEpsPer = 1, 6
        this%fDGamma = dGammaCvg
        idx1 = idxi(iEpsPer)
        idx2 = idxj(iEpsPer)
        FTotInc = UNITMAT
        FTotInc(idx1, idx2) = FTotInc(idx1, idx2) + dltEpsPer(idx1, idx2) 
        if (idx1 /= idx2) FtotInc(idx2, idx1) = FtotInc(idx1, idx2)

        FTotPer = matmul(FTotInc, this%fFTotNew)
        call this%UpdateStress(crysOldStateVars, crysNewStateVars, stage)
        sigEpsPer = this%fCrysSig
        rplEpsPer = this%fCrysRpl

        if (this%fNewDt < 1.0d0) then
          this%fCrysCijkl    = ten3333ToA66(this%fCijkl)
          this%fCrysDRplDEps = 0.0d0
          exit
        else
          this%fCrysCijkl(:, iEpsPer) = (sigEpsPer - sigCvg)/kEpsIncPer
          this%fCrysDRplDEps(iEpsPer) = (rplEpsPer - rplCvg)/kEpsIncPer 
        end if

      end do


   !   dEpsDt = (/-2.3d-5, -2.3d-5, -2.3d-5, 0.0d0, 0.0d0, 0.0d0/) 
   !   this%fDSigDTemp = matmul(this%fDSigDEps, dEpsDT)
   !   this%fDRplDTemp = sum(this%fDRplDEps*dEpsDT)


   !   ! Pertubation of tempertature
   !   this%fDGamma = dGammaCvg
   !   tempPer = tempNew + kTempIncPer
   !   call this%UpdateStress(FTotOld, FTotNew, tempOld, tempPer, statevOld, statevNew, nstatv, dtime, stage)
   !   sigTempPerPlus = this%fCrysSig
   !   rplTempPerPlus = this%fRpl

   !   this%fDGamma = dGammaCvg
   !   tempPer = tempNew - kTempIncPer
   !   call this%UpdateStress(FTotOld, FTotNew, tempOld, tempPer, statevOld, statevNew, nstatv, dtime, stage)
   !   sigTempPerMinus = this%fCrysSig
   !   rplTempPerMinus = this%fRpl

   !   if (this%fNewDt < 1.0d0) then
   !     this%fDSigDTemp = 0.0d0
   !     this%fDRplDTemp = 0.0d0
   !   else
   !     this%fDSigDTemp = (sigTempPerPlus - sigTempPerMinus)/(2.0*kTempIncPer)
   !     this%fDRplDTemp = (rplTempPerPlus - rplTempPerMinus)/(2.0*kTempIncPer)
   !   end if

   !   this%fCrysSig = sigCvg 
   !   this%fRpl    = RplCvg 

    end subroutine Pertubation




    subroutine PreStep(this, stage)
      use algebra, only : ten4Rot, multQBQt

      class(CrysPlasMatPt) :: this
      integer(kind = IKIND), intent(in) :: stage

      real(kind = RKIND) :: Cijkl(3, 3, 3, 3)
      real(kind = RKIND) :: Schmidt(3, 3, this%fNumSlipSys)

      integer(kind = IKIND) :: i



      Cijkl       = this%fCrysPlasMat%GetElasticModuli(this%fTempNew)
      this%fCijkl = ten4Rot(Cijkl, this%fRotMatx)

      Schmidt     = this%fCrysPlasMat%GetSchmidt()
      do i = 1, this%fNumSlipSys
        this%fSchmidt(:, :, i) = multQBQt(Schmidt(:, :, i), this%fRotMatx)
      end do


    end subroutine PreStep




    subroutine SolveStep(this, stage)
      use algebra, only : vecNorm, GaussJordan

      class(CrysPlasMatPt) :: this
      integer(kind = IKIND), intent(in) :: stage
  
      real(kind = RKIND), parameter :: kBigNorm = 1.0d10
  
      real(kind = RKIND) :: LHS(9+this%fNumSlipSys, 9+this%fNumSlipSys)
      real(kind = RKIND) :: RHS(9+this%fNumSlipSys)
      real(kind = RKIND) :: XX(9+this%fNumSlipSys)
      real(kind = RKIND) :: DX(9+this%fNumSlipSys)
      real(kind = RKIND) :: auxIter(18)

      real(kind = RKIND)    :: normRHS
      real(kind = RKIND)    :: normDX
      real(kind = RKIND)    :: pNewDt
      real(kind = RKIND)    :: iterTol
      integer(kind = IKIND) :: nIters
      integer(kind = IKIND) :: iter
  
      logical(kind = LKIND) :: processed
      integer(kind = IKIND) :: iflag
  

      integer(kind = IKIND), parameter :: maxItersSig = 100
      integer(kind = IKIND), parameter :: maxItersJac = 40
      real(kind    = RKIND), parameter :: tolerSig    = 1.0d-10
      real(kind    = RKIND), parameter :: tolerJac    = 1.0d-11

      
      call this%InitXX(stage, XX, auxIter)
  
      if (stage == kConvgIter) then
        nIters  = maxItersSig
        iterTol = tolerSig
      else if (stage == kPertubation) then
        nIters  = maxItersJac
        iterTol = tolerJac
      end if
  
      pNewdt    = 1.0d0
      iter      = 1
      processed = .false.
      do while (iter <= nIters)
        call this%FormRHS(auxIter, RHS, pNewDt)
        this%fNewDt = min(this%fNewDt, pNewDt)
        if (this%fNewDt < 1.0d0) return
        normRHS = vecNorm(RHS)
    !    write(*, *) "stage = ", stage, "iter = ", iter, "normRHS = ", normRHS
        if (normRHS <= iterTol) exit
        if (normRHS > kBigNorm .and. .not. processed) then
          call this%Correction(stage, auxIter, XX, pNewDt)
          iter      = 0
          processed = .true.
        else
          call this%FormLHS(auxIter, LHS)
          call GaussJordan(LHS, RHS, DX, iflag)
          if (iflag == 1)  pNewDt = min(pNewDt, 0.5d0)
          
          call this%UpdateXX(auxIter, XX, DX)
          normDX = vecNorm(DX)
          if (normDX > kBigNorm)  then
            call this%Correction(stage, auxIter, XX, pNewDt)
          end if
  
        end if

        iter = iter + 1

        this%fNewDt = min(this%fNewDt, pNewDt)
        if (this%fNewDt < 1.0d0) return 

      end do
  
      if (iter >= nIters .and. stage == kConvgIter) then
        write(*, *) "Max iterations reached!"
        pNewDt      = 0.50d0
        this%fNewDt = min(this%fNewDt, pNewDt)
      end if
  
    end subroutine SolveStep




    subroutine PostStep(this, stage)
      use algebra,   only : multQBQt, matDet, matInv, polarDcmp, UNITMAT

      class(CrysPlasMatPt) :: this
      integer(kind = IKIND), intent(in) :: stage

      real(kind = RKIND), parameter :: disFrc = 0.0d0

      real(kind = RKIND) :: elsGrn(3, 3)
      real(kind = RKIND) :: sigPK2(3, 3)
      real(kind = RKIND) :: sigCauchy(3, 3)
      real(kind = RKIND) :: FTotInc(3, 3)
      real(kind = RKIND) :: FThNew(3, 3)
      real(kind = RKIND) :: FElsPrd(3, 3)
      real(kind = RKIND) :: det, RR(3, 3), UU(3, 3)

      integer(kind = IKIND) :: i, idxi(6), idxj(6)



      if (this%fNewDt < 1.0d0) return

      if (stage == kConvgIter) then
        FTotInc  = matmul(this%fFTotNew, matInv(this%fFTotOld))
        FElsPrd  = matmul(FTotInc, this%fFElsOld) 
        this%fLPlsNew = (UNITMAT - matmul(matInv(FElsPrd), this%fFElsNew))/this%fDt
      end if

      if (this%fIsTempDep) then
        FThNew  = this%fCrysPlasMat%GetThermalDeformationGradient(this%fTempNew)
      else
        FThNew  = UNITMAT
      end if
     ! elsGrn    = this%GreenStrain(matmul(this%fFElsNew, matInv(FThNew)))
      elsGrn    = this%GreenStrain(this%fFElsNew)
      sigPK2    = this%ElasticStress(elsGrn - (FThNew - UNITMAT))
      sigCauchy = multQBQt(sigPK2, this%fFElsNew)
      det       = matDet(this%fFElsNew)
      sigCauchy = sigCauchy/det

      idxi = (/1, 2, 3, 1, 1, 2/)
      idxj = (/1, 2, 3, 2, 3, 3/)
      do i = 1, 6
        this%fCrysSig(i) = sigCauchy(idxi(i), idxj(i))
      end do
      
      this%fTauResl  = this%ResolvedSlipStress(sigPK2)
      this%fGammaDot = this%fCrysPlasMat%GetSlipRate(this%fTauResl, this%fTauCritNew, this%fTempNew)
      this%fCrysRpl  = disFrc*sum(dabs(this%fTauResl*this%fGammaDot))
      
      
    end subroutine PostStep
      



    subroutine InitXX(this, stage, XX, auxIter)
      use algebra, only : UNITMAT, matInv, polarDcmp

      class(CrysPlasMatPt) :: this
      integer(kind = IKIND), intent(in) :: stage
      real(kind = RKIND)   :: XX(9+this%fNumSlipSys)
      real(kind = RKIND)   :: auxIter(18)

      real(kind = RKIND) :: FTotInc(3, 3)
      real(kind = RKIND) :: FElsPrd(3, 3)
      real(kind = RKIND) :: FThNew(3, 3)
      real(kind = RKIND) :: elsEGrn(3, 3)
      real(kind = RKIND) :: sigPK2(3, 3)
      real(kind = RKIND) :: RR(3, 3)
      real(kind = RKIND) :: UU(3, 3)


      FTotInc  = matmul(this%fFTotNew, matInv(this%fFTotOld))
      FElsPrd  = matmul(FTotInc, this%fFElsOld) 

      if (stage == kConvgIter) then
        this%fFElsNew = matmul(FElsPrd, UNITMAT - this%fLPlsOld*this%fDt)
      end if


      XX(1:  9) = reshape(this%fFElsNew, (/9/))
      XX(10: 9+this%fNumSlipSys) = this%fDGamma

     
      auxIter(1:9) = reshape(matInv(FElsPrd), (/9/))

      call polarDcmp(FTotInc, RR, UU)
      auxIter(10:18) = reshape(matmul(RR, this%fFTotOld), (/9/))


    end subroutine InitXX

   

    ! Calculate the residual (right hand section(RHS) of the equation) in each N-R iteration  
    subroutine FormRHS(this, auxIter, RHS, pNewDt)
      use algebra, only : vecNorm, matInv, UNITMAT
  
      class(CrysPlasMatPt) :: this
      real(kind = RKIND), intent(in)  :: auxIter(18)
      real(kind = RKIND), intent(out) :: RHS(9+this%fNumSlipSys)
      real(kind = RKIND)              :: pNewDt
  
      real(kind = RKIND) :: FThNew(3, 3)
      real(kind = RKIND) :: elsEGrn(3, 3)
      real(kind = RKIND) :: sigPK2(3, 3)
      real(kind = RKIND) :: LPlsCur(3, 3)
      real(kind = RKIND) :: FElsPrdInv(3, 3)
  
      if (this%fIsTempDep) then
        FThNew  = this%fCrysPlasMat%GetThermalDeformationGradient(this%fTempNew)
      else
        FThNew  = UNITMAT
      end if

     ! elsEGrn       = this%GreenStrain(matmul(this%fFElsNew, matInv(FThNew)))
      elsEGrn       = this%GreenStrain(this%fFElsNew)
      sigPK2        = this%ElasticStress(elsEGrn - (FThNew - UNITMAT))
      this%fTauResl = this%ResolvedSlipStress(sigPK2)
      if (this%fCrysPlasMat%GetStressDivergenceState(this%fTauResl, this%fTauCritNew)) then
        write(*, *) "Diverged tauResl/tauCrit detected!"
        pNewDt = min(pNewDt, 0.75d0)
        return
      end if

      this%fGammaDot = this%fCrysPlasMat%GetSlipRate(this%fTauResl, this%fTauCritNew, this%fTempNew)
      LPlsCur  = this%VelocityGradientPlastic(this%fGammaDot)


      FElsPrdInv    = reshape(auxIter(1:9), (/3, 3/))
      !this%fLPlsNew = (UNITMAT - matmul(FElsPrdInv, this%fFElsNew))/this%fDt
      this%fLPlsNew = UNITMAT - matmul(FElsPrdInv, this%fFElsNew)
  
      RHS(1: 9)  = reshape(this%fLPlsNew - LPlsCur*this%fDt, (/9/))
      RHS(10: 9+this%fNumSlipSys) = this%fDGamma - this%fGammaDot*this%fDt
     

    end subroutine FormRHS



    subroutine FormLHS(this, auxIter, LHS)
      use algebra, only : UNITMAT, tenmul, MultAijmnBmnkl, ten3333ToA99

      class(CrysPlasMatPt) :: this
      real(kind = RKIND), intent(in) :: auxIter(18)
      real(kind = RKIND) :: LHS(9+this%fNumSlipSys, 9+this%fNumSlipSys)

      real(kind = RKIND) :: FThNew(3, 3)
  
      real(kind = RKIND) :: dEdFe(3, 3, 3, 3)
      real(kind = RKIND) :: dGammaDotDFe(3, 3, this%fNumSlipSys)
  
      real(kind = RKIND) :: dGammaDotDTauResl(this%fNumSlipSys)
      real(kind = RKIND) :: dGammaDotDTauCrit(this%fNumSlipSys)
  
      real(kind = RKIND) :: dTauCritDGamma(this%fNumSlipSys, this%fNumSlipSys)
      real(kind = RKIND) :: dTauCritAux(this%fNumSlipSys, this%fNumSlipSys)
      real(kind = RKIND) :: FElsPrdInv(3, 3)
      real(kind = RKIND) :: auxTen4th1(3, 3, 3, 3), auxTen4th2(3, 3, 3, 3), auxTen4th3(3, 3, 3, 3)
      real(kind = RKIND) :: auxMatx(3, 3), aux1
      integer(kind = IKIND) :: nSlipSys
      integer(kind = IKIND) :: i, j, k, l
  
  
      nSlipSys = this%fNumSlipSys
  
      if (this%fIsTempDep) then
        FThNew  = this%fCrysPlasMat%GetThermalDeformationGradient(this%fTempNew)
      else
        FThNew  = UNITMAT
      end if

      dGammaDotDTauResl = this%fCrysPlasMat%GetDSlipRateDTauResl(this%fTauResl, this%fTauCritNew, this%fTempNew)
      dGammaDotDTauCrit = this%fCrysPlasMat%GetDSlipRateDTauCrit(this%fTauResl, this%fTauCritNew, this%fTempNew)
      !BOX11: partial Lp / partial Fe
      auxTen4th1 = 0.0d0
      FElsPrdInv = reshape(auxIter(1:9), (/3, 3/))
      do i = 1, 3
        do j = 1, 3
         ! aux1 = -FElsPrdInv(i, j)/this%fDt
          aux1 = -FElsPrdInv(i, j)
          
          auxTen4th1(i, 1, j, 1) = aux1
          auxTen4th1(i, 2, j, 2) = aux1
          auxTen4th1(i, 3, j, 3) = aux1
        end do
      end do
  
      do i = 1, 3
        do j = 1, 3
          do k = 1, 3
            do l = 1, 3
              dEdFe(i, j, k, l) = 0.5d0*(UNITMAT(i, l)*this%fFElsNew(k, j) + UNITMAT(j, l)*this%fFElsNew(k, i))
            end do
          end do
        end do
      end do
  
      auxTen4th2 = MultAijmnBmnkl(this%fCijkl, dEdFe) 
      auxTen4th3 = 0.0d0
      do i = 1, nSlipSys
        do j = 1, 3
          do k = 1, 3
            dGammaDotDFe(j, k, i) = dGammaDotDTauResl(i) * sum(this%fSchmidt(:, :, i)*auxTen4th2(:, :, j, k))
          end do
        end do
        !auxTen4th3 = auxTen4th3 + tenmul(this%fSchmidt(:, :, i), dGammaDotDFe(:, :, i))
        auxTen4th3 = auxTen4th3 + this%fDt*tenmul(this%fSchmidt(:, :, i), dGammaDotDFe(:, :, i))
      end do
    
       LHS(1:9, 1:9) = Ten3333ToA99(auxTen4th1 - auxTen4th3)
  
      !BOX12: Partial Lp/ partial dgamma
      dTauCritDGamma = this%fCrysPlasMat%GetDTauCritDGamma(this%fGammaNew, this%fGammaDot, this%fTauCritNew, this%fTempNew)
      do i = 1, nSlipSys
        do j = 1, nSlipSys
          dTauCritAux(i, j) = dGammaDotDTauCrit(i)*dTauCritDGamma(i, j)
        end do
      end do
  
      do j = 1, nSlipSys
        auxMatx = 0.0d0
        do i = 1, nSlipSys
          !auxMatx = auxMatx + this%fSchmidt(:, :, j)*dTauCritAux(j, i)
          auxMatx = auxMatx - this%fDt*this%fSchmidt(:, :, i)*dTauCritAux(i, j)
        end do
        LHS(1:9, 9+j) = reshape(auxMatx, (/9/))
      end do
  
      !BOX21: Partial dgamma / partial Fe
      do i = 1, nSlipSys
        LHS(9+i, 1:9) = reshape(-this%fDt*dGammaDotDFe(:, :, i), (/9/))
      end do
  
      ! BOX22: Partial dgamma/dgamma
      LHS(10:9+nSlipSys, 10:9+nSlipSys) = -dTauCritAux*this%fDt
      do i = 1, nSlipSys
        LHS(9+i, 9+i) = LHS(9+i, 9+i) + 1.0d0
      end do
  
    end subroutine FormLHS
  
  



    subroutine UpdateXX(this, auxIter, XX, dX) 
      use algebra, only : UNITMAT

      class(CrysPlasMatPt) :: this 
      real(kind = RKIND), intent(in) :: auxIter(18)
      real(kind = RKIND)             :: XX(9+this%fNumSlipSys)
      real(kind = RKIND), intent(in) :: dX(9+this%fNumSlipSys)
  
      real(kind = RKIND) :: dTauCrit(this%fNumSlipSys)
      real(kind = RKIND) :: FElsPrdInv(3, 3)
  
  
      XX = XX - dX
  
      this%fFElsNew  = reshape(XX(1:9), (/3, 3/))
      this%fDGamma   = XX(10: 9+this%fNumSlipSys)

      this%fGammaNew = this%fGammaOld + dabs(this%fDGamma)
  
      dTauCrit = this%fCrysPlasMat%GetTauCritRate(this%fGammaNew, this%fDGamma, this%fTauCritNew, this%fTempNew)
      this%fTauCritNew = this%fTauCritOld + dTauCrit
  
      FElsPrdInv    = reshape(auxIter(1:9), (/3, 3/))
      this%fLPlsNew = (UNITMAT - matmul(FElsPrdInv, this%fFElsNew))/this%fDt

    end subroutine UpdateXX
  


    ! NUMERICAL PROBLEMS WITH LARGE CORRECTIONS-->PLASTIC PREDICTOR
    subroutine Correction(this, stage, auxIter, XX, pNewDt)
      use algebra,  only : matInv, UNITMAT

      class(crysPlasMatPt) :: this
      integer(kind = IKIND), intent(in) :: stage
      real(kind = RKIND),    intent(in) :: auxIter(18)
      real(kind = RKIND)                :: XX(9+this%fNumSlipSys)
      real(kind = RKIND)                :: pNewDt
  
      real(kind = RKIND) :: FThNew(3, 3)
      real(kind = RKIND) :: elsEGrn(3, 3)
      real(kind = RKIND) :: sigPK2(3, 3)
      real(kind = RKIND) :: tauCritDot(this%fNumSlipSys)
  
 
      this%fFElsNew =  reshape(auxIter(10:18), (/3, 3/))
      if (stage == kConvgIter) then
        if (this%fIsTempDep) then
          FThNew  = this%fCrysPlasMat%GetThermalDeformationGradient(this%fTempNew)
        else
          FThNew  = UNITMAT
        end if
     !   elsEGrn       = this%GreenStrain(matmul(this%fFElsNew, matInv(FThNew)))
        elsEGrn       = this%GreenStrain(this%fFElsNew)
        sigPK2        = this%ElasticStress(elsEGrn - (FThNew - UNITMAT))
        this%fTauResl = this%ResolvedSlipStress(sigPK2)
  
        if (this%fCrysPlasMat%GetStressDivergenceState(this%fTauResl, this%fTauCritNew)) then
          write(*, *) "Diverged tauResl/tauCrit detected!"
          pNewDt = min(pNewDt, 0.75d0)
          return
        end if
    
        this%fGammaDot = this%fCrysPlasMat%GetSlipRate(this%fTauResl, this%fTauCritNew, this%fTempNew)
        this%fDGamma   = this%fGammaDot*this%fDt
        this%fGammaNew = this%fGammaOld + dabs(this%fDGamma)
    
        tauCritDot = this%fCrysPlasMat%GetTauCritRate(this%fGammaNew, this%fGammaDot, this%fTauCritNew, this%fTempNew)
        this%fTauCritNew = this%fTauCritOld + tauCritDot*this%fDt
  
      else if (stage == kPertubation) then
        this%fDGamma     = 0.0d0
        this%fGammaNew   = this%fGammaOld + dabs(this%fDGamma)
        this%fTauCritNew = this%fTauCritOld
      end if

      XX(1:  9) = reshape(this%fFElsNew, (/9/))
      XX(10: 9+this%fNumSlipSys) = this%fDGamma

    end subroutine Correction




    function GreenStrain(this, FF) result(epsGrn)
      use algebra, only : UNITMAT

      class(CrysPlasMatPt), intent(in) :: this
      real(kind = RKIND),   intent(in) :: FF(3, 3)
      real(kind = RKIND) :: epsGrn(3, 3)

      real(kind = RKIND) :: CC(3, 3)

      CC     = matmul(transpose(FF), FF)
      epsGrn = 0.5d0*(CC - UNITMAT) 

    end function GreenStrain



    function ElasticStress(this, epsGrn) result(sigma)
      use algebra, only : multCijklEkl

      class(CrysPlasMatPt), intent(in) :: this
      real(kind = RKIND),   intent(in) :: epsGrn(3, 3)
      real(kind = RKIND) :: sigma(3, 3)

      sigma = multCijklEkl(this%fCijkl, epsGrn)

    end function ElasticStress




    function ResolvedSlipStress(this, sigma) result(tauResl)
      class(CrysPlasMatPt), intent(in) :: this
      real(kind = RKIND),   intent(in) :: sigma(3, 3)
      real(kind = RKIND)               :: tauResl(this%fNumSlipSys)

      real(kind = RKIND)    :: auxMatx(3, 3)
      integer(kind = IKIND) :: i

      do i = 1, this%fNumSlipSys
        auxMatx = this%fSchmidt(:, :, i)
        tauResl(i) = sum(sigma*auxMatx)
      end do

    end function

        

    function VelocityGradientPlastic(this, gammaDot) result(Lp)
      class(CrysPlasMatPt), intent(in) :: this
      real(kind = RKIND),   intent(in) :: gammaDot(this%fNumSlipSys)
      real(kind = RKIND)               :: Lp(3, 3)

      integer(kind = IKIND) :: i

      Lp(1:3, 1:3) = 0.0d0
      do i = 1, this%fNumSlipSys
        Lp = Lp + gammaDot(i)*this%fSchmidt(:, :, i)
      end do

    end function VelocityGradientPlastic

      
  end module typeCrysPlasMatPt

